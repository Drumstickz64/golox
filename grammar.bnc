program        -> declaration* EOF ;

declaration    -> varDeclaration
                | statement ;

varDeclaration -> "var" IDENTIFIER ( "=" expression )? ";" ;

statement      -> exprStmt
                | ifStmt
                | printStmt
                | block ;

block          -> "{" declaration* "}" ;

ifStmt         -> "if" "(" expression ")" statement
                  ( "else" statement )? ;

exprStmt       -> expression ";" ;
printStmt      -> "print" expression ";" ;

expression     -> assignment ;
assignment     -> IDENTIFIER "=" expression
                | logical_or;

logical_or     -> logical_and ( "or" logical_and )* ;
logical_and    -> equality ( "and" equality )* ;

equality       -> comparison ( ( "!=" | "==" ) comparison )* ;
comparison     -> term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term           -> factor ( ( "-" | "+" ) factor )* ;
factor         -> unary ( ( "/" | "*" ) unary )* ;
unary          -> ( "!" | "-" ) unary
                | primary ;
primary        -> NUMBER | STRING
                | "true" | "false" | "nil"
                | "(" expression ")"
                | IDENTIFIER ;